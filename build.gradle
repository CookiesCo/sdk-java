plugins {
    id "java-library"
    id "maven-publish"
    id "com.google.cloud.artifactregistry.gradle-plugin" version "2.1.4"
    id "com.github.ben-manes.versions" version "0.39.0"
    id "org.sonarqube" version "3.3"
    id "jacoco"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group 'co.cookies.sdk'
archivesBaseName = "sdk-java"
version 'v1.2021r1-beta3'

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    api group: 'com.google.api', name: 'gax', version: '1.66.0'
    api group: 'com.google.api', name: 'gax-grpc', version: '1.66.0'
    api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    api group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    api group: 'co.cookies.sdk', name: 'gapic-sdk-catalog-v1-java', version: 'v1.2021r1beta1'
    api group: 'co.cookies.sdk.grpc', name: 'grpc-sdk-catalog-v1-java', version: 'v1.2021r1beta1'
    api group: 'co.cookies.sdk.proto', name: 'proto-sdk-catalog-v1-java', version: 'v1.2021r1beta1'
    api group: 'co.cookies.sdk', name: 'proto-catalog', version: 'v1.2021r1beta1'
    api group: 'co.cookies.sdk', name: 'proto-cookies', version: 'v1.2021r1beta1'
    compileOnly group: 'com.google.auto.value', name: 'auto-value-annotations', version: '1.8.2'
    runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.6'
    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: '1.8.2'
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "com.google.truth.extensions:truth-proto-extension:1.1.3"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

sonarqube {
    properties {
        property "sonar.projectKey", "CookiesCo_sdk-java"
        property "sonar.organization", "cookies"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name "cookies"
            url "artifactregistry://us-maven.pkg.dev/cookies-co/sdk-java"
        }
        maven {
            name "opencannabis"
            url "artifactregistry://us-maven.pkg.dev/opencannabis-sdk/java"
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        (repository.name == findProperty("artifactRepository"))
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

repositories {
    maven {
        name "cookies"
        url "artifactregistry://us-maven.pkg.dev/cookies-co/sdk-java"
    }
    maven {
        name "opencannabis"
        url "artifactregistry://us-maven.pkg.dev/opencannabis-sdk/java"
    }
    google()
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

test {
    useJUnitPlatform()
}

check.dependsOn jacocoTestReport

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

if (hasProperty("ungate") || hasProperty("ungate-stage")) {
    nexusPublishing {
        repositories {
            sonatype {
                nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
                snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            }
        }
    }
}
